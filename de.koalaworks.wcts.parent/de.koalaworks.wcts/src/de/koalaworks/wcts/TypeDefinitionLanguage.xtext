grammar de.koalaworks.wcts.TypeDefinitionLanguage with org.eclipse.xtext.common.Terminals

import "http://www.eclipse.org/emf/2002/Ecore" as ecore

generate typeDefinitionLanguage "http://www.koalaworks.de/wcts/TypeDefinitionLanguage"

SiteStructure:
	(typeDefinitions+=Type)*;

Type:
	FeatureCapableType | ReferenceType;

FeatureCapableType:
	ContentType | PageType;

ContentType:
	'content' 'type' name=ID
	('is' 'recognized' 'by' selector=ContentSelector)?
	('recognize' (features+=Feature)+)?;

PageType:
	'page' 'type' name=ID
	'is' 'recognized' 'by' selector=PageSelector
	('recognize' (features+=Feature)+)?;

ReferenceType:
	'reference' 'type' name=ID
	('is' 'recognized' 'by' selector=ReferenceSelector)?;

ContentSelector:
	CssSelector;

PageSelector:
	CssSelector | UrlPatternSelector;

ReferenceSelector:
	CssSelector | UrlPatternSelector;

CssSelector:
	'css' definition=SELECTOR_VALUE;

UrlPatternSelector:
	'url' 'pattern' definition=SELECTOR_VALUE;

Feature:
	ScalarFeature | CollectionFeature;	

ScalarFeature:
	name=ID 'as' type=[FeatureType] ('by' selector=(FeatureSelector))?;	

CollectionFeature:
	name=ID 'as' 'many' type=[FeatureType] ('each' 'by' selector=(FeatureSelector))?;

FeatureType:
	ContentType | ReferenceType;

FeatureSelector:
	CssSelector | UrlPatternSelector;

terminal SELECTOR_VALUE:
	'«' (!('\b' | '\f' | '\n' | '\r' | '\t' ))* '»';
