grammar de.koalaworks.wcts.TypeDefinitionLanguage with org.eclipse.xtext.common.Terminals

generate typeDefinitionLanguage "http://www.koalaworks.de/wcts/TypeDefinitionLanguage"

SiteStructure:
	(typeDefinitions+=Type)*;

Type:
	ContentType | PageType | ReferenceType;

ContentType:
	'content' 'type' name=ID
	('is' 'recognized' 'by' selector=ContentSelector)?
	('recognize' (properties+=Property)*)?;

FeatureType:
	ContentType | ReferenceType;

PageType:
	'page' 'type' name=ID
	'is' 'recognized' 'by' selector=PageSelector
	('recognize' (properties+=Property)*)?;

ReferenceType:
	'reference' 'type' name=ID
	('is' 'recognized' 'by' selector=ReferenceSelector)?
	('recognize' (properties+=Property)*)?;

ContentSelector:
	CssSelector;

FeatureSelector:
	CssSelector | UrlPatternSelector;

PageSelector:
	CssSelector | UrlPatternSelector;

ReferenceSelector:
	CssSelector | UrlPatternSelector;

CssSelector:
	'css' definition=STRING;

UrlPatternSelector:
	'url' 'pattern' definition=STRING;

// TODO Semantic check that ensures that referenced feature type and selector fit
Property:
	name=ID 'as' type=[FeatureType] ('by' selector=(FeatureSelector))?;