grammar de.koalaworks.wcts.TypeDefinitionLanguage with org.eclipse.xtext.common.Terminals

generate typeDefinitionLanguage "http://www.koalaworks.de/wcts/TypeDefinitionLanguage"

SiteStructure:
	(typeDefinitions+=Type)*;

Type:
	ContentType | PageType | ReferenceType;

ContentType:
	'content' 'type' name=ID
	('is' 'recognized' 'by' selector=ContentSelector)?
	('recognize' (features+=Feature)*)?;

PageType:
	'page' 'type' name=ID
	'is' 'recognized' 'by' selector=PageSelector
	('recognize' (features+=Feature)*)?;

ReferenceType:
	'reference' 'type' name=ID
	('is' 'recognized' 'by' selector=ReferenceSelector)?
	('recognize' (features+=Feature)*)?;

ContentSelector:
	CssSelector;

PageSelector:
	CssSelector | UrlPatternSelector;

ReferenceSelector:
	CssSelector | UrlPatternSelector;

CssSelector:
	'css' definition=STRING;

UrlPatternSelector:
	'url' 'pattern' definition=STRING;

// TODO Semantic check that ensures that referenced feature type and selector fit
// TODO Semantic check that ensures that references have no references
Feature:
	name=ID 'as' type=[FeatureType] ('by' selector=(FeatureSelector))?;

FeatureType:
	ContentType | ReferenceType;

FeatureSelector:
	CssSelector | UrlPatternSelector;